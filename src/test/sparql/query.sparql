PREFIX m:   <http://openmodelica.org/openmodelica/classes#>

SELECT DISTINCT ?m ?con1 ?con2

WHERE {
	?m a m:Model .
	?m m:contains ?con1 .
	?m m:contains ?con2 .
	
	?con1 a m:Connector .
	?con1 m:hasName "p" .
	?con1 m:contains [
		a m:Component ;
		m:hasName "v" ;
		m:hasType [
			a m:Real ;
			m:hasQuantity "ElectricPotential" ;
			m:hasUnit "V"
		]
	] .
	?con1 m:contains [
		a m:Component ;
		m:hasName "i" ;
		m:hasPrefix "flow" ;
		m:hasType [
			a m:Real ;
			m:hasQuantity "ElectricCurrent" ;
			m:hasUnit "A"
		]
	] .
	
	?con2 a m:Connector .
	?con2 m:hasName "n" .
	?con2 m:contains [
		a m:Component ;
		m:hasName "v" ;
		m:hasType [
			a m:Real ;
			m:hasQuantity "ElectricPotential" ;
			m:hasUnit "V"
		]
	] .
	?con2 m:contains [
		a m:Component ;
		m:hasName "i" ;
		m:hasPrefix "flow" ;
		m:hasType [
			a m:Real ;
			m:hasQuantity "ElectricCurrent" ;
			m:hasUnit "A"
		]
	] .
	
	# If we do not look for specific names for con1 / con2, uncomment the following statement
	#FILTER (?con1 != ?con2)
}