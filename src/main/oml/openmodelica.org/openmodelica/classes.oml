@dc:title "OpenModelica Classes"
vocabulary <http://openmodelica.org/openmodelica/classes#> as m {

	extends <http://www.w3.org/2001/XMLSchema#> as xsd

	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs

	extends <http://purl.org/dc/elements/1.1/> as dc

	aspect Named

	scalar property hasName [
		domain Named
		range xsd:string
		functional
	]

	scalar property hasComment [
		domain Named
		range xsd:string
	]

	aspect Namespace :> Named

	relation entity Contains [
		from Namespace
		to Named
		forward contains
		reverse isContainedBy
	]

	concept Class :> Namespace

	concept Model :> Class

	concept Block :> Class

	concept Record :> Class

	concept Connector :> Class

	concept Function :> Class

	concept Package :> Class

	structure Type
	
	structured property hasType [
		domain Component
		range Type
	]

	structure PrimitiveType :> Type

	scalar property hasQuantity [
		domain PrimitiveType
		range xsd:string
	]

	structure Real :> PrimitiveType
	
	scalar property hasUnit [
		domain Real
		range xsd:string
	]

	scalar property hasDisplayUnit [
		domain Real
		range xsd:string
	]

	scalar property hasStartDecimal [
		domain Real
		range xsd:decimal
	]

	structure Integer :> PrimitiveType

	structure Boolean :> PrimitiveType

	structure String :> PrimitiveType

	concept Component :> Named

	enumerated scalar Prefix [
		"input",
		"output",
		"parameter",
		"constant",
		"replaceable",
		"flow",
		"stream",
		"inner",
		"outer",
		"innerouter",
		"final"
	]

	scalar property hasPrefix [
		domain Component
		range Prefix
	]

	scalar property hasInitialValue [
		domain Component
		range rdfs:Literal
	]

}